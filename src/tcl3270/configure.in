dnl Copyright 2000, 2001 by Paul Mattes.
dnl  Permission to use, copy, modify, and distribute this software and its
dnl  documentation for any purpose and without fee is hereby granted,
dnl  provided that the above copyright notice appear in all copies and that
dnl  both that copyright notice and this permission notice appear in
dnl  supporting documentation.

dnl Process this file with autoconf to produce a configure script.
AC_INIT(tcl3270.c)

dnl Checks for programs.
AC_PROG_INSTALL
AC_PROG_CC

dnl Figure out what sort of host this is.
dnl If it's solaris, then pass the -D__EXTENSIONS__ flag to cc, so that
dnl all of usual Unix functions are visible to strict ANSI compilers.
AC_CANONICAL_HOST
case "$host_os" in
solaris2*)	XANSI=-D__EXTENSIONS__
		;;
darwin*)	XPRECOMP=-no-cpp-precomp
		;;
*)		XANSI=""
		;;
esac
AC_SUBST(XANSI)
AC_SUBST(XPRECOMP)

dnl Allow the tcl version to change
AC_ARG_WITH(tcl, [  --with-tcl=VERSION      Specify Tcl library version [8.3]],
tclver=$with_tcl, tclver=8.3)

dnl Allow TclX
AC_ARG_WITH(tclx, [  --with-tclx[=VERSION]   Use TclX], , with_tclx=no)

dnl Set up LDFLAGS, CPPFLAGS and tclvr (tclver without the period, for BSD)
dnl Note that --with-tclx=VERSION overrides --with-tcl=VERSION.
if test "$with_tclx" = no
then	:
elif test "$with_tclx" != yes
then	tclver=$with_tclx
fi
CPPFLAGS="$CPPFLAGS -I/usr/local/include/tcl$tclver -I/usr/local/include"
LDFLAGS="$LDFLAGS -L/usr/local/lib"
tclvr=`echo $tclver | tr -d .`

dnl Checks for orindary library functions.
dnl AC_PROG_GCC_TRADITIONAL
dnl AC_FUNC_MEMCMP
dnl AC_TYPE_SIGNAL
dnl AC_FUNC_VPRINTF
dnl AC_CHECK_FUNCS(gettimeofday putenv select socket strerror strstr strtol strtoul)
AC_CHECK_FUNCS(vasprintf)
AC_FUNC_FSEEKO

dnl Check for libraries.
dnl Note that the order here is important.  The last libraries should appear
dnl first, so that objects in them can be used by subsequent libraries.
AC_CHECK_LIB(util, forkpty)
AC_CHECK_LIB(tcl$tclver, Tcl_Init, ,
 AC_CHECK_LIB(tcl$tclvr, Tcl_Init, ,AC_ERROR(Can't find TCL library), -lm), -lm)
if test "$with_tclx" != no
then	AC_CHECK_LIB(tclx$tclver, Tclx_Init, ,
 AC_CHECK_LIB(tclx$tclvr, Tclx_Init, ,AC_ERROR(Can't find TclX library), -lm), -lm)
fi
AC_CHECK_LIB(nsl, gethostbyname)
AC_CHECK_LIB(socket, socket)

dnl Checks for header files.
dnl AC_HEADER_STDC
dnl AC_HEADER_SYS_WAIT
dnl AC_CHECK_HEADERS(fcntl.h sys/ioctl.h sys/time.h unistd.h)
AC_CONFIG_HEADER(conf.h)
AC_CHECK_HEADERS(sys/select.h)
AC_CHECK_HEADERS(pty.h)
AC_CHECK_HEADERS(libutil.h)
AC_CHECK_HEADERS(util.h)
AC_CHECK_HEADERS(tcl.h, ,AC_ERROR(Can't find TCL header file))
if test "$with_tclx" != no
then	AC_CHECK_HEADERS(tclExtend.h, ,AC_ERROR(Can't find TclX header file))
fi

dnl Checks for typedefs, structures, and compiler characteristics.
dnl AC_C_CONST
dnl AC_TYPE_PID_T
dnl AC_TYPE_SIZE_T
dnl AC_HEADER_TIME

dnl Set up the configuration directory.
LIBX3270DIR='${sysconfdir}/x3270'
AC_SUBST(LIBX3270DIR)

dnl Check for unwanted parts.
AC_ARG_ENABLE(ansi,[  --disable-ansi          leave out NVT (ANSI) support])
case "$enable_ansi" in
""|yes)	AC_DEFINE(X3270_ANSI,1)
	;;
esac
AC_ARG_ENABLE(apl,[  --disable-apl           leave out APL character support])
case "$enable_apl" in
""|yes)	AC_DEFINE(X3270_APL,1)
	;;
esac
AC_ARG_ENABLE(ft,[  --disable-ft            leave out file transfer support])
case "$enable_ft" in
""|yes)	AC_DEFINE(X3270_FT,1)
	;;
esac
AC_ARG_ENABLE(local_process,[  --disable-local-process leave out local process support])
case "$enable_local_process" in
""|yes)	AC_DEFINE(X3270_LOCAL_PROCESS,1)
	;;
esac
AC_ARG_ENABLE(menus,[  --disable-menus         leave out menu support])
case "$enable_menus" in
""|yes)	AC_DEFINE(X3270_MENUS,1)
	;;
esac
AC_ARG_ENABLE(tn3270e,[  --disable-tn3270e       leave out TN3270E support])
case "$enable_tn3270e" in
""|yes)	AC_DEFINE(X3270_TN3270E,1)
	;;
esac
AC_ARG_ENABLE(trace,[  --disable-trace         leave out tracing support])
case "$enable_trace" in
""|yes)	AC_DEFINE(X3270_TRACE,1)
	;;
esac

dnl Generate the Makefile.
AC_OUTPUT(Makefile)
