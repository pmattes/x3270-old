dnl Copyright 2000, 2001, 2002 by Paul Mattes.
dnl  Permission to use, copy, modify, and distribute this software and its
dnl  documentation for any purpose and without fee is hereby granted,
dnl  provided that the above copyright notice appear in all copies and that
dnl  both that copyright notice and this permission notice appear in
dnl  supporting documentation.
dnl 
dnl x3270 is distributed in the hope that it will be useful, but WITHOUT ANY
dnl WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
dnl FOR A PARTICULAR PURPOSE.  See the file LICENSE for more details.

dnl Process this file with autoconf to produce a configure script.
AC_INIT(main.c)
AC_CONFIG_HEADER(conf.h)

dnl Checks for programs.
AC_PROG_INSTALL
AC_PROG_CC

dnl Figure out what sort of host this is.
dnl If it's solaris, then pass the -D__EXTENSIONS__ flag to cc, so that
dnl all of usual Unix functions are visible to strict ANSI compilers.
AC_CANONICAL_HOST
case "$host_os" in
solaris2*)	XANSI=-D__EXTENSIONS__
		if test "$GCC" = yes
		then	NIXCCOPTIONS="CCOPTIONS ="
		fi
		;;
darwin*)	XPRECOMP=-no-cpp-precomp
		;;
linux*)		XANSI=-D__USE_BSD
		;;
esac
AC_SUBST(XANSI)
AC_SUBST(XPRECOMP)
AC_SUBST(NIXCCOPTIONS)

dnl Check for X
AC_PATH_X
if test "$no_x" = "yes"; then AC_MSG_ERROR('Cannot find X utilities or libraries'); fi

dnl Check for --with-all-xinstall
AC_ARG_WITH(all_xinstall,[  --with-all-xinstall     install all components in system X11 directory])
case "$with_all_xinstall" in
yes)	prefix='$(PROJECTROOT)'
	;;
esac

dnl Check for --enable-app-defaults
AC_ARG_ENABLE(app-defaults,
    [  --enable-app-defaults   use a separate app-defaults file],
    [DEFINE_UAD="#define USE_APP_DEFAULTS 1"; if test "$enable_app_defaults" = "yes"; then UAD_FLAGS="-DCOLOR"; else UAD_FLAGS="$enable_app_defaults"; fi],
    [DEFINE_UAD=""; UAD_FLAGS="-DCOLOR"])
AC_SUBST(UAD_FLAGS)
AC_SUBST(DEFINE_UAD)

dnl Check for --without-pr3287
AC_ARG_WITH(pr3287,[  --without-pr3287        don't build pr3287])
if test -d pr3287 -a "$with_pr3287" != "no"
then	DEFINE_PR3287="#define PR3287 1"
	pr3mf=pr3287/Makefile
else	DEFINE_PR3287="#undef PR3287"
	pr3mf=""
fi
AC_SUBST(DEFINE_PR3287)

dnl Check for --with-fontdir=DIR
AC_ARG_WITH(fontdir,[  --with-fontdir=DIR      install fonts in directory DIR [DIR=misc]])
case "$with_fontdir" in
""|yes|no)
	IFONTDIR='$'"(FONTDIR)/misc"
	CIFONTDIR='$'"(exec_prefix)/fonts/misc"
	;;
/*)
	IFONTDIR="$with_fontdir"
	CIFONTDIR="$with_fontdir"
	;;
*)
	IFONTDIR='$'"(FONTDIR)/$with_fontdir"
	CIFONTDIR='$'"(exec_prefix)/fonts/$with_fontdir"
	;;
esac
AC_SUBST(IFONTDIR)
AC_SUBST(CIFONTDIR)

# Set up the configuration directory.
LIBX3270DIR='${sysconfdir}/x3270'
AC_SUBST(LIBX3270DIR)

dnl Check for unwanted parts.
AC_ARG_ENABLE(ansi,[  --disable-ansi          leave out NVT (ANSI) support])
case "$enable_ansi" in
""|yes)	AC_DEFINE(X3270_ANSI,1)
	;;
esac
AC_ARG_ENABLE(apl,[  --disable-apl           leave out APL character support])
case "$enable_apl" in
""|yes)	AC_DEFINE(X3270_APL,1)
	;;
esac
AC_ARG_ENABLE(dbcs,[  --disable-dbcs          leave out DBCS support])
AC_ARG_ENABLE(ft,[  --disable-ft            leave out file transfer support])
case "$enable_ft" in
""|yes)	AC_DEFINE(X3270_FT,1)
	;;
esac
AC_ARG_ENABLE(keypad,[  --disable-keypad        leave out pop-up keypad support])
case "$enable_keypad" in
""|yes)	AC_DEFINE(X3270_KEYPAD,1)
	;;
esac
AC_ARG_ENABLE(local_process,[  --disable-local-process leave out local process support])
case "$enable_local_process" in
""|yes)	AC_DEFINE(X3270_LOCAL_PROCESS,1)
	;;
esac
AC_ARG_ENABLE(menus,[  --disable-menus         leave out menu support])
case "$enable_menus" in
""|yes)	AC_DEFINE(X3270_MENUS,1)
	;;
esac
AC_ARG_ENABLE(printer,[  --disable-printer       leave out printer session support])
case "$enable_printer" in
""|yes)	AC_DEFINE(X3270_PRINTER,1)
	;;
esac
AC_ARG_ENABLE(script,[  --disable-script        leave out scripting support])
case "$enable_script" in
""|yes)	AC_DEFINE(X3270_SCRIPT,1)
	;;
*)	X3270_SCRIPT="#undef X3270_SCRIPT" ;;
esac
AC_ARG_ENABLE(tn3270e,[  --disable-tn3270e       leave out TN3270E support])
case "$enable_tn3270e" in
""|yes)	AC_DEFINE(X3270_TN3270E,1)
	;;
esac
AC_ARG_ENABLE(trace,[  --disable-trace         leave out tracing support])
case "$enable_trace" in
""|yes)	AC_DEFINE(X3270_TRACE,1)
	;;
esac

dnl Check for other libraries.
dnl Note that the order here is important.  The last libraries should appear
dnl first, so that objects in them can be used by subsequent libraries.
AC_CHECK_LIB(util, forkpty)
AC_CHECK_LIB(nsl_s, gethostbyname)
AC_CHECK_LIB(nsl, gethostbyname)
AC_CHECK_LIB(socket, socket)

dnl Checks for header files.
dnl AC_HEADER_STDC
dnl AC_HEADER_SYS_WAIT
dnl AC_CHECK_HEADERS(fcntl.h sys/ioctl.h sys/time.h unistd.h)
AC_CHECK_HEADERS(sys/select.h)
AC_CHECK_HEADERS(pty.h)
AC_CHECK_HEADERS(libutil.h)
AC_CHECK_HEADERS(util.h)
AC_CHECK_HEADERS(getopt.h)

dnl Checks for typedefs, structures, and compiler characteristics.
dnl AC_C_CONST
dnl AC_TYPE_PID_T
dnl AC_TYPE_SIZE_T
dnl AC_HEADER_TIME

dnl Checks for library functions.
dnl AC_PROG_GCC_TRADITIONAL
dnl AC_FUNC_MEMCMP
dnl AC_TYPE_SIGNAL
dnl AC_FUNC_VPRINTF
dnl AC_CHECK_FUNCS(gettimeofday putenv select socket strerror strstr strtol strtoul)
AC_CHECK_FUNCS(vasprintf)
AC_CHECK_FUNC(strtok_r,,AC_LIBOBJ(strtok_r)
AC_DEFINE(NEED_STRTOK_R))
AC_FUNC_FSEEKO

AC_ARG_ENABLE(ssl,[  --disable-ssl           leave out OpenSSL support])

checkssldir() { :
	if test -f "$1/include/openssl/ssl.h"
	then	ssldir="$1"
		return 0
	else	return 1
	fi
}

AC_ARG_WITH(ssl, [  --with-ssl=DIR          location of installed OpenSSL libraries/include files], [checkssldir "$withval"], )

if test "$enable_ssl" != "no"
then	dnl Check for SSL.
	AC_MSG_CHECKING([for OpenSSL library install directory])
	if test -z "$ssldir"
	then	for basedir in /usr/local /usr/lib /usr/pkg /usr /var/ssl /opt
		do	for dir in $basedir $basedir/ssl
			do	checkssldir $dir && break 2
			done
		done
	fi
	if test -z "$ssldir"
	then	AC_MSG_RESULT([Not found])
		echo
		echo "Couldn't find OpenSSL library installation directory."
		echo "Use the --with-ssl option to specify it."
		exit 1
	fi
	AC_MSG_RESULT([$ssldir])
	if test "$ssldir" != /usr
	then	CPPFLAGS="$CPPFLAGS -I$ssldir/include"
		LDFLAGS="$LDFLAGS -L$ssldir/lib"
	fi

	dnl Check for -lssl.
	AC_CHECK_LIB(crypto, CRYPTO_malloc, , [AC_MSG_ERROR(Can't find libcrypto)], )
	AC_CHECK_LIB(ssl, SSL_new, , [AC_MSG_ERROR(Can't find libssl)], )
	SSL=-DHAVE_LIBSSL
fi
AC_SUBST(SSL)

checkicudir() { :
	if test -f "$1/include/unicode/ucnv.h"
	then	icudir="$1"
		return 0
	else	return 1
	fi
}

AC_ARG_WITH(icu, [  --with-icu=DIR          location of installed ICU libraries/include files], [checkicudir "$withval"], )

if test "$enable_dbcs" != "no"
then	dnl Check for ICU.
	AC_MSG_CHECKING([for ICU library install directory])
	if test -z "$icudir"
	then	for basedir in /usr/local /usr/lib /usr/pkg /usr /var/icu /opt
		do	for dir in $basedir $basedir/icu
			do	checkicudir $dir && break 2
			done
		done
	fi
	if test -z "$icudir"
	then	AC_MSG_RESULT([Not found])
		echo
		echo "Couldn't find ICU library installation directory."
		echo "Use the --with-icu option to specify it."
		enable_dbcs="no"	# insufficient, hmm
	else	AC_MSG_RESULT([$icudir])
		if test "$icudir" != /usr
		then	CPPFLAGS="$CPPFLAGS -I$icudir/include"
			LDFLAGS="$LDFLAGS -L$icudir/lib"
		fi

		dnl Check for -licui18n.
		icu_ver_file=$icudir/include/unicode/uversion.h
		if test ! -f $ivu_ver_file
		then	AC_MSG_ERROR(Can't find ICU version file)
		fi
		icu_suffix=`awk '/#define.*U_ICU_VERSION_SUFFIX/ {print $3}' $icu_ver_file`
		if test -z "$icu_suffix"
		then	AC_MSG_ERROR(Can't determine ICU version)
		fi
		AC_CHECK_LIB(icui18n, ucnv_open${icu_suffix}, , [enable_dbcs="no"], )
		if test "$enable_dbcs" = "no"
		then	echo "Can't find libicui18n"
		else
			dnl Figure out the ICU data suffix.
			dnl Todo: This needs some error recovery.
			AC_MSG_CHECKING([for ICU data prefix])
			cat >/tmp/icutst$$.c <<EOF
#include "unicode/ucnv.h"

int
main(int argc, char *argv[])
{
	printf("%s\n", U_ICUDATA_NAME);
	return 0;
}
EOF
			${CC} ${CPPFLAGS} -o /tmp/icutst$$ /tmp/icutst$$.c
			rm -f /tmp/icutst$$.c
			ICU_DATA_PREFIX=`/tmp/icutst$$`
			rm -f /tmp/icutst$$
			AC_MSG_RESULT([$ICU_DATA_PREFIX])

			if test -z "$ICU_DATA_PREFIX"
			then	AC_MSG_WARN(Cannot determine ICU data prefix)
				enable_dbcs="no"
			else
				dnl Check for xmkmkf
				AC_CHECK_PROG(makeconv_found,makeconv,found,notfound)
				if test "$makeconv_found" = "notfound"
				then	AC_MSG_WARN(Cannot find makeconv)
					enable_dbcs="no"
				fi
			fi
		fi

	fi
	if test "$enable_dbcs" = "no"
	then	AC_MSG_WARN(Disabling DBCS.)
	fi
fi
case "$enable_dbcs" in
no)	;;
*)	AC_DEFINE(X3270_DBCS,1)
	DBCS_C="wide.c"
	DBCS_O="wide.o"
	INSTALL_CNV="#define INSTALL_CNV 1"
	;;
esac
AC_SUBST(DBCS_C)
AC_SUBST(DBCS_O)
AC_SUBST(INSTALL_CNV)
AC_SUBST(ICU_DATA_PREFIX)

dnl Check for xmkmkf
AC_CHECK_PROG(xmkmf_found,xmkmf,found,notfound)
if test "$xmkmf_found" = "notfound"
then	AC_MSG_ERROR('Cannot find xmkmf')
fi

dnl Save a copy of Imakefile
if test -f Imakefile
then	cp Imakefile Imakefile.old
fi

dnl Generate the Imakefile and the pr3287 Makefile.
AC_OUTPUT(Imakefile $pr3mf)

dnl Generate the Makefile
gen_Makefile=""
if test ! -f Makefile; then gen_Makefile=1; fi
if cmp Imakefile.old Imakefile 1>/dev/null 2>/dev/null; then :; else gen_Makefile=1; fi
if test "$gen_Makefile"
then	echo "generating Makefile"
	xmkmf || exit 1
	# Get rid of the @ that AIX puts in the Makefile
	sed 's/@@*for/for/' Makefile >Makefile.aixfix
	if cmp -s Makefile Makefile.aixfix
	then	rm Makefile.aixfix
	else	mv Makefile.aixfix Makefile
	fi
else	echo "Makefile is unchanged"
fi
rm -f Imakefile.old
